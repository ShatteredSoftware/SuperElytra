import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven{ url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    implementation 'io.sentry:sentry:6.0.0'
}

// Calculates the git hash and adds it to the version.
ext.getHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

processResources {
    def hash = "${getHash()}"
    with copySpec{
        from 'src/main/resources'
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        filter(ReplaceTokens, tokens: [ version: project.version ])
    }
}

group = 'eisenwave.elytra'
version = "${project.version}-${getHash()}"
sourceCompatibility = '1.8'

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

task jars {
    dependsOn shadowJar, jar
}

shadowJar {
    dependencies {
        include(dependency('io.sentry:sentry:.*'))
    }
    relocate 'io.sentry', 'eisenwave.elytra.deps'
    minimize()
    classifier 'all'
}